#!/usr/bin/env python

Import("env")

env_png = env.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_libpng"]:
    third_party_dir = "#third-party/libpng/"
    third_party_sources = [
        "png.c",
        "pngerror.c",
        "pngget.c",
        "pngmem.c",
        "pngpread.c",
        "pngread.c",
        "pngrio.c",
        "pngrtran.c",
        "pngrutil.c",
        "pngset.c",
        "pngtrans.c",
        "pngwio.c",
        "pngwrite.c",
        "pngwtran.c",
        "pngwutil.c",
    ]
    third_party_sources = [third_party_dir + file for file in third_party_sources]

    env_png.Prepend(CPPPATH=[third_party_dir])
    # Needed for drivers includes and in platforms/web
    env.Prepend(CPPPATH=[third_party_dir])

    # Currently .ASM filter_neon.S does not compile on NT.
    import os

    use_neon = "neon_enabled" in env and env["neon_enabled"] and os.name != "nt"
    if use_neon:
        env_png.Append(CPPDEFINES=[("PNG_ARM_NEON_OPT", 2)])
    else:
        env_png.Append(CPPDEFINES=[("PNG_ARM_NEON_OPT", 0)])

    env_third_party = env_png.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)

    if use_neon:
        env_neon = env_third_party.Clone()
        if "S_compiler" in env:
            env_neon["CC"] = env["S_compiler"]
        neon_sources = []
        neon_sources.append(env_neon.Object(third_party_dir + "/arm/arm_init.c"))
        neon_sources.append(
            env_neon.Object(third_party_dir + "/arm/filter_neon_intrinsics.c")
        )
        neon_sources.append(env_neon.Object(third_party_dir + "/arm/filter_neon.S"))
        neon_sources.append(
            env_neon.Object(third_party_dir + "/arm/palette_neon_intrinsics.c")
        )
        third_party_obj += neon_sources

    env.drivers_sources += third_party_obj


# Rebel source files

driver_obj = []

env_png.add_source_files(driver_obj, "*.cpp")
env.drivers_sources += driver_obj

# Needed to force rebuilding the driver files when the third-party library is updated.
env.Depends(driver_obj, third_party_obj)
