apply plugin: 'maven-publish'
apply plugin: 'signing'

group = ossrhGroupId
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId ossrhGroupId
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Rebel Engine Android Library'
                    url = 'https://rebeltoolbox.com/'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/RebelToolbox/RebelEngine/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'madmiraal'
                            name = 'Marcel Admiraal'
                            email = 'info@rebeltoolbox.com'
                        }
                    }

                    // Version control info - if you're using GitHub, follow the format as seen here
                    scm {
                        connection = 'scm:git:github.com/RebelToolbox/RebelEngine.git'
                        developerConnection = 'scm:git:ssh://github.com/RebelToolbox/RebelEngine.git'
                        url = 'https://github.com/RebelToolbox/RebelEngine/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
        rootProject.ext["signing.keyId"],
        rootProject.ext["signing.key"],
        rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
