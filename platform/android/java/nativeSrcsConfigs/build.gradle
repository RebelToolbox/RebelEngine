// Non functional android library used to provide Android Studio editor support to the project.
plugins {
    alias libs.plugins.androidLibrary
}

android {
    namespace 'com.rebeltoolbox.rebelengine'
    compileSdk versions.compileSdk

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    ndkVersion versions.ndkVersion

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    libraryVariants.configureEach { variant ->
        def buildType = variant.buildType.name.capitalize()

        def taskPrefix = ""
        if (project.path != ":") {
            taskPrefix = project.path + ":"
        }

        // We use SCons to build the native libraries.
        // So, we need to disable the externalNativeBuild* tasks.
        gradle.startParameter.excludedTaskNames += taskPrefix + "externalNativeBuild" + buildType
    }
}

dependencies {}
