// Non functional android library used to provide Android Studio editor support to the project.
plugins {
    alias libs.plugins.androidLibrary
}

android {
    namespace 'com.rebeltoolbox.rebelengine'
    compileSdk versions.compileSdk

    defaultConfig {
        minSdk versions.minSdk
        targetSdk versions.targetSdk
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE', 'META-INF/NOTICE']
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }

    ndkVersion versions.ndkVersion

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    libraryVariants.all { variant ->
        def buildType = variant.buildType.name.capitalize()

        def taskPrefix = ""
        if (project.path != ":") {
            taskPrefix = project.path + ":"
        }

        // Disable the externalNativeBuild* task as it would cause build failures since the cmake build
        // files is only setup for editing support.
        gradle.startParameter.excludedTaskNames += taskPrefix + "externalNativeBuild" + buildType
    }
}

dependencies {}
