#!/usr/bin/env python

Import("env")
Import("env_modules")

env_squish = env_modules.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_squish"]:
    third_party_dir = "#third-party/libsquish/"
    third_party_sources = [
        "alpha.cpp",
        "clusterfit.cpp",
        "colourblock.cpp",
        "colourfit.cpp",
        "colourset.cpp",
        "maths.cpp",
        "rangefit.cpp",
        "singlecolourfit.cpp",
        "squish.cpp",
    ]

    third_party_sources = [third_party_dir + file for file in third_party_sources]

    env_squish.Prepend(CPPPATH=[third_party_dir])

    env_third_party = env_squish.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)
    env.modules_sources += third_party_obj


# Rebel source files

module_obj = []

env_squish.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
