#!/usr/bin/env python

Import("env")

import os

import modules_builders

env_modules = env.Clone()

Export("env_modules")

env.Depends("modules_enabled.gen.h", Value(env.modules_path))
env.CommandNoCache(
    "modules_enabled.gen.h",
    Value(env.modules_path),
    modules_builders.generate_modules_enabled,
)

env.modules_sources = []
env_modules.add_source_files(env.modules_sources, "register_module_types.gen.cpp")

for module_name, path in env.modules_path.items():
    if not os.path.isabs(path):
        SConscript(module_name + "/SCsub")  # Built-in.
    else:
        SConscript(path + "/SCsub")  # Custom.

if env["split_libmodules"]:
    env.split_lib("modules", env_lib=env_modules)
else:
    lib = env_modules.add_library("modules", env.modules_sources)

    env.Prepend(LIBS=[lib])
