#!/usr/bin/env python

Import("env")
Import("env_modules")

env_enet = env_modules.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_enet"]:
    third_party_dir = "#third-party/enet/"
    third_party_sources = [
        "rebel_enet.cpp",
        "callbacks.c",
        "compress.c",
        "host.c",
        "list.c",
        "packet.c",
        "peer.c",
        "protocol.c",
    ]
    third_party_sources = [third_party_dir + file for file in third_party_sources]

    env_enet.Prepend(CPPPATH=[third_party_dir])
    env_enet.Append(CPPDEFINES=["REBEL_ENET"])

    env_third_party = env_enet.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)
    env.modules_sources += third_party_obj


# Rebel source files

module_obj = []

env_enet.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
