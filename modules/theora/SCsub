#!/usr/bin/env python

Import("env")
Import("env_modules")

env_theora = env_modules.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_libtheora"]:
    third_party_dir = "#third-party/libtheora/"
    third_party_sources = [
        # "analyze.c",
        # "apiwrapper.c",
        "bitpack.c",
        "cpu.c",
        # "decapiwrapper.c",
        "decinfo.c",
        "decode.c",
        "dequant.c",
        # "encapiwrapper.c",
        # "encfrag.c",
        # "encinfo.c",
        # "encode.c",
        # "encoder_disabled.c",
        # "enquant.c",
        # "fdct.c",
        "fragment.c",
        "huffdec.c",
        # "huffenc.c",
        "idct.c",
        "info.c",
        "internal.c",
        # "mathops.c",
        # "mcenc.c",
        "quant.c",
        # "rate.c",
        "state.c",
        # "tokenize.c",
    ]

    third_party_sources_x86 = [
        # "x86/mmxencfrag.c",
        # "x86/mmxfdct.c",
        "x86/mmxfrag.c",
        "x86/mmxidct.c",
        "x86/mmxstate.c",
        # "x86/sse2fdct.c",
        # "x86/x86enc.c",
        "x86/x86state.c",
    ]

    third_party_sources_x86_vc = [
        # "x86_vc/mmxencfrag.c",
        # "x86_vc/mmxfdct.c",
        "x86_vc/mmxfrag.c",
        "x86_vc/mmxidct.c",
        "x86_vc/mmxstate.c",
        # "x86_vc/x86enc.c",
        "x86_vc/x86state.c",
    ]

    if env["x86_libtheora_opt_gcc"]:
        third_party_sources += third_party_sources_x86

    if env["x86_libtheora_opt_vc"]:
        third_party_sources += third_party_sources_x86_vc

    if env["x86_libtheora_opt_gcc"] or env["x86_libtheora_opt_vc"]:
        env_theora.Append(CPPDEFINES=["OC_X86_ASM"])

    third_party_sources = [third_party_dir + file for file in third_party_sources]

    env_theora.Prepend(CPPPATH=[third_party_dir])

    # also requires libogg and libvorbis
    if env["builtin_libogg"]:
        env_theora.Prepend(CPPPATH=["#third-party/libogg"])
    if env["builtin_libvorbis"]:
        env_theora.Prepend(CPPPATH=["#third-party/libvorbis"])

    env_third_party = env_theora.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)
    env.modules_sources += third_party_obj


# Rebel source files

module_obj = []

env_theora.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
