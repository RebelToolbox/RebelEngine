#!/usr/bin/env python

Import("env")
Import("env_modules")

env_etc = env_modules.Clone()

# Third-party source files

third_party_obj = []

# Not unbundled so far since not widespread as shared library
third_party_dir = "#third-party/etc2comp/"
third_party_sources = [
    "EtcBlock4x4.cpp",
    "EtcBlock4x4Encoding.cpp",
    "EtcBlock4x4Encoding_ETC1.cpp",
    "EtcBlock4x4Encoding_R11.cpp",
    "EtcBlock4x4Encoding_RG11.cpp",
    "EtcBlock4x4Encoding_RGB8A1.cpp",
    "EtcBlock4x4Encoding_RGB8.cpp",
    "EtcBlock4x4Encoding_RGBA8.cpp",
    "Etc.cpp",
    "EtcDifferentialTrys.cpp",
    "EtcFilter.cpp",
    "EtcImage.cpp",
    "EtcIndividualTrys.cpp",
    "EtcMath.cpp",
    "EtcSortedBlockList.cpp",
]
third_party_sources = [third_party_dir + file for file in third_party_sources]

env_etc.Prepend(CPPPATH=[third_party_dir])

env_third_party = env_etc.Clone()
env_third_party.disable_warnings()
env_third_party.add_source_files(third_party_obj, third_party_sources)
env.modules_sources += third_party_obj

# Rebel source files

module_obj = []

env_etc.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
