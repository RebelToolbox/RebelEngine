#!/usr/bin/env python

Import("env")
Import("env_modules")

env_upnp = env_modules.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_miniupnpc"]:
    third_party_dir = "#third-party/miniupnp/miniupnpc/"
    third_party_sources = [
        "addr_is_reserved.c",
        "connecthostport.c",
        "igd_desc_parse.c",
        "minisoap.c",
        "minissdpc.c",
        "miniupnpc.c",
        "miniwget.c",
        "minixml.c",
        "portlistingparse.c",
        "receivedata.c",
        "upnpcommands.c",
        "upnpdev.c",
        "upnpreplyparse.c",
    ]
    third_party_sources = [
        third_party_dir + "src/" + file for file in third_party_sources
    ]

    env_upnp.Prepend(CPPPATH=[third_party_dir + "include/"])
    env_upnp.Append(CPPDEFINES=["MINIUPNP_STATICLIB"])
    if env["platform"] != "windows":
        env_upnp.Append(CPPDEFINES=["MINIUPNPC_SET_SOCKET_TIMEOUT"])

    env_third_party = env_upnp.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)
    env.modules_sources += third_party_obj


# Rebel source files

module_obj = []

env_upnp.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
