#!/usr/bin/env python

Import("env")
Import("env_modules")

env_tinyexr = env_modules.Clone()

# Third-party source files

third_party_obj = []

# Not unbundled for now as they are not commonly available as shared library
third_party_dir = "#third-party/tinyexr/"
third_party_sources = [
    "tinyexr.cc",
]
third_party_sources = [third_party_dir + file for file in third_party_sources]

env_tinyexr.Prepend(CPPPATH=[third_party_dir])

# Enable threaded loading with C++11.
env_tinyexr.Append(CPPDEFINES=["TINYEXR_USE_THREAD"])

env_third_party = env_tinyexr.Clone()
env_third_party.disable_warnings()
env_third_party.add_source_files(third_party_obj, third_party_sources)
env.modules_sources += third_party_obj

# Rebel source files

module_obj = []

env_tinyexr.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
