#!/usr/bin/env python

Import("env")
Import("env_modules")

env_pvr = env_modules.Clone()

# Third-party source files

third_party_obj = []

# Not unbundled so far since not widespread as shared library
third_party_dir = "#third-party/pvrtccompressor/"
third_party_sources = [
    "BitScale.cpp",
    "MortonTable.cpp",
    "PvrTcDecoder.cpp",
    "PvrTcEncoder.cpp",
    "PvrTcPacket.cpp",
]
third_party_sources = [third_party_dir + file for file in third_party_sources]

env_pvr.Prepend(CPPPATH=[third_party_dir])

env_third_party = env_pvr.Clone()
env_third_party.disable_warnings()
env_third_party.add_source_files(third_party_obj, third_party_sources)
env.modules_sources += third_party_obj

# Rebel source files

module_obj = []

env_pvr.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
