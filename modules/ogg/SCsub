#!/usr/bin/env python

Import("env")
Import("env_modules")

# Only kept to build the third-party library used by the theora and webm
# modules.

env_ogg = env_modules.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_libogg"]:
    third_party_dir = "#third-party/libogg/"
    third_party_sources = [
        "bitwise.c",
        "framing.c",
    ]
    third_party_sources = [third_party_dir + file for file in third_party_sources]

    env_ogg.Prepend(CPPPATH=[third_party_dir])

    env_third_party = env_ogg.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)
    env.modules_sources += third_party_obj


# Rebel source files

module_obj = []

env_ogg.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
