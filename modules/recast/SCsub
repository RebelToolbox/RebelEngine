#!/usr/bin/env python

Import("env")
Import("env_modules")

env_recast = env_modules.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_recast"]:
    third_party_dir = "#third-party/recastnavigation/Recast/"
    third_party_sources = [
        "Source/Recast.cpp",
        "Source/RecastAlloc.cpp",
        "Source/RecastArea.cpp",
        "Source/RecastAssert.cpp",
        "Source/RecastContour.cpp",
        "Source/RecastFilter.cpp",
        "Source/RecastLayers.cpp",
        "Source/RecastMesh.cpp",
        "Source/RecastMeshDetail.cpp",
        "Source/RecastRasterization.cpp",
        "Source/RecastRegion.cpp",
    ]
    third_party_sources = [third_party_dir + file for file in third_party_sources]

    env_recast.Prepend(CPPPATH=[third_party_dir + "/Include"])

    env_third_party = env_recast.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)
    env.modules_sources += third_party_obj

# Rebel source files

module_obj = []

env_recast.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
