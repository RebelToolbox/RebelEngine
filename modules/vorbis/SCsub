#!/usr/bin/env python

Import("env")
Import("env_modules")

# Only kept to build the third-party library used by the theora and webm
# modules. We now use stb_vorbis for AudioStreamOGGVorbis.

env_vorbis = env_modules.Clone()

# Third-party source files

third_party_obj = []

if env["builtin_libvorbis"]:
    third_party_dir = "#third-party/libvorbis/"
    third_party_sources = [
        # "analysis.c",
        # "barkmel.c",
        "bitrate.c",
        "block.c",
        "codebook.c",
        "envelope.c",
        "floor0.c",
        "floor1.c",
        "info.c",
        "lookup.c",
        "lpc.c",
        "lsp.c",
        "mapping0.c",
        "mdct.c",
        "psy.c",
        # "psytune.c",
        "registry.c",
        "res0.c",
        "sharedbook.c",
        "smallft.c",
        "synthesis.c",
        # "tone.c",
        # "vorbisenc.c",
        "vorbisfile.c",
        "window.c",
    ]

    third_party_sources = [third_party_dir + file for file in third_party_sources]

    env_vorbis.Prepend(CPPPATH=[third_party_dir])

    # also requires libogg
    if env["builtin_libogg"]:
        env_vorbis.Prepend(CPPPATH=["#third-party/libogg"])

    env_third_party = env_vorbis.Clone()
    env_third_party.disable_warnings()
    env_third_party.add_source_files(third_party_obj, third_party_sources)
    env.modules_sources += third_party_obj


# Rebel source files

module_obj = []

env_vorbis.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the third-party library is updated.
env.Depends(module_obj, third_party_obj)
