#!/usr/bin/env python

Import("env")

env_crypto = env.Clone()

is_builtin = env["builtin_mbedtls"]
has_module = env["module_mbedtls_enabled"]
third_party_obj = []

# If we're using the builtin third-party Mbed TLS library,
# but we're not using the full Mbed TLS module,
# we need to include the path to the Mbed TLS header files.
if is_builtin or not has_module:
    env_crypto.Prepend(CPPPATH=["#third-party/mbedtls/include"])

# If we're not using the full Mbed TLS module,
# we need to create a custom build of a subset of the Mbed TLS library.
if not has_module:
    env_third_party = env_crypto.Clone()
    env_third_party.disable_warnings()
    # Custom config file
    env_third_party.Append(
        CPPDEFINES=[
            (
                "MBEDTLS_CONFIG_FILE",
                '\\"core/crypto/mbedtls_config.h\\"',
            )
        ]
    )
    third_party_mbedtls_dir = "#third-party/mbedtls/library/"
    third_party_mbedtls_sources = [
        "aes.c",
        "base64.c",
        "constant_time.c",
        "md5.c",
        "sha1.c",
        "sha256.c",
    ]
    third_party_mbedtls_sources = [
        third_party_mbedtls_dir + file for file in third_party_mbedtls_sources
    ]
    third_party_mbedtls_sources.append("mbedtls_platform_util.c")
    env_third_party.add_source_files(third_party_obj, third_party_mbedtls_sources)
    env.core_sources += third_party_obj


# Rebel source files

core_obj = []

env_crypto.add_source_files(core_obj, "*.cpp")
env.core_sources += core_obj

# Needed to force rebuilding the core files when the third-party library is updated.
env.Depends(core_obj, third_party_obj)
